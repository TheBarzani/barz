START -> PROG .
PROG -> CLASSIMPLFUNC PROG | .
CLASSIMPLFUNC -> CLASSDECL | IMPLDEF | FUNCDEF .
CLASSDECL -> class id ISA1 lcurbr VISMEMBERDECL rcurbr semi .
VISMEMBERDECL -> VISIBILITY MEMDECL VISMEMBERDECL | .
ISA1 -> isa id ISA2 | .
ISA2 -> comma id ISA2 | .
IMPLDEF -> implementation id lcurbr IMPLBODY rcurbr .
IMPLBODY -> FUNCDEF IMPLBODY | .
FUNCDEF -> FUNCHEAD FUNCBODY .
VISIBILITY -> public | private .
MEMDECL -> FUNCDECL | ATTRDECL .
FUNCDECL -> FUNCHEAD semi .
FUNCHEAD -> function id lpar FPARAMS rpar arrow RETURNTYPE
| constructor lpar FPARAMS rpar .
FUNCBODY -> lcurbr LOCALVARDECLORSTAT2 rcurbr .
LOCALVARDECLORSTAT -> LOCALVARDECL | STATEMENT .
LOCALVARDECLORSTAT2 -> LOCALVARDECLORSTAT LOCALVARDECLORSTAT2 | .
ATTRDECL -> attribute VARDECL .
LOCALVARDECL -> local VARDECL .
VARDECL -> id colon TYPE ARRAYSIZES semi .
STATEMENT -> FUNCALLORASSIGN semi
| if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi
| while lpar RELEXPR rpar STATBLOCK semi
| read lpar VARIABLE rpar semi
| write lpar EXPR rpar semi
| return lpar EXPR rpar semi .
FUNCALLORASSIGN -> IDORSELF FUNCALLORASSIGN2 .
FUNCALLORASSIGN2 -> INDICES FUNCALLORASSIGN3 | lpar APARAMS rpar FUNCALLORASSIGN4 .
FUNCALLORASSIGN3 -> ASSIGNOP EXPR | dot id FUNCALLORASSIGN2 .
FUNCALLORASSIGN4 -> dot id FUNCALLORASSIGN2 | .
STATBLOCK -> lcurbr STATEMENTS rcurbr | STATEMENT | .
STATEMENTS -> STATEMENT STATEMENTS | .
EXPR -> ARITHEXPR EXPR2 .
EXPR2 -> RELOP ARITHEXPR | .
RELEXPR -> ARITHEXPR RELOP ARITHEXPR .
ARITHEXPR -> TERM RIGHTRECARITHEXPR .
RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR | .
SIGN -> plus | minus .
TERM -> FACTOR RIGHTRECTERM .
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM | .
FACTOR -> IDORSELF FACTOR2 REPTVARIABLEORFUNCTIONCALL
| floatlit
| intlit
| lpar ARITHEXPR rpar
| not FACTOR
| SIGN FACTOR .
FACTOR2 -> lpar APARAMS rpar | INDICES .
INDICES -> INDICE INDICES | .
REPTVARIABLEORFUNCTIONCALL -> IDNEST REPTVARIABLEORFUNCTIONCALL | .
VARIABLE -> IDORSELF VARIABLE2 .
VARIABLE2 -> INDICES REPTVARIABLE | lpar APARAMS rpar VARIDNEST .
REPTVARIABLE -> VARIDNEST REPTVARIABLE | .
VARIDNEST -> dot id VARIDNEST2 .
VARIDNEST2 -> lpar APARAMS rpar VARIDNEST | INDICES .
INDICE -> lsqbr ARITHEXPR rsqbr .
IDNEST -> dot id IDNEST2 .
IDNEST2 -> lpar APARAMS rpar | INDICES .
ARRAYSIZE -> lsqbr ARRAYSIZE2 .
ARRAYSIZE2 -> intlit rsqbr | rsqbr .
ARRAYSIZES -> ARRAYSIZE ARRAYSIZES | .
TYPE -> int | float | id .
RETURNTYPE -> TYPE | void .
APARAMS -> EXPR REPTAPARAMS1 | .
REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1 | .
APARAMSTAIL -> comma EXPR .
FPARAMS -> id colon TYPE ARRAYSIZES REPTFPARAMS1 | .
REPTFPARAMS1 -> FPARAMSTAIL REPTFPARAMS1 | .
FPARAMSTAIL -> comma id colon TYPE ARRAYSIZES .
ASSIGNOP -> assign .
RELOP -> eq | neq | lt | gt | lteq | gteq .
ADDOP -> plus | minus | or .
MULTOP -> mult | div | and .
IDORSELF -> id | self .

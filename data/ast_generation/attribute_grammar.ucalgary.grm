START -> _createLists PROG _mergeLists . 

ADDOP -> plus  . 
ADDOP -> minus  . 
ADDOP -> or  . 

APARAMS -> EXPR REPTAPARAMS1  . 
APARAMS ->  . 

APARAMSTAIL -> comma EXPR  . 

ARITHEXPR -> TERM RIGHTRECARITHEXPR  . 

ARRAYSIZE -> lsqbr ARRAYSIZE2  .

ARRAYSIZE2 -> intlit rsqbr .
ARRAYSIZE2 -> rsqbr .

ATTRIBUTEDECL -> attribute VARDECL  . 

CLASSDECL -> class id OPTCLASSDECL2 lcurbr REPTCLASSDECL4 rcurbr semi  . 

CLASSORIMPLORFUNC -> CLASSDECL _createClass . 
CLASSORIMPLORFUNC -> IMPLDEF _createImplementation . 
CLASSORIMPLORFUNC -> FUNCDEF _createFunction .

EXPR -> ARITHEXPR EXPR2 .

EXPR2 -> RELOP ARITHEXPR .
EXPR2 ->  .

FACTOR -> IDORSELF FACTOR2 REPTVARIABLEORFUNCTIONCALL .  
FACTOR -> intlit .  
FACTOR -> floatlit .  
FACTOR -> lpar ARITHEXPR rpar .  
FACTOR -> not FACTOR .  
FACTOR -> SIGN FACTOR .  

FACTOR2 -> lpar APARAMS rpar .  
FACTOR2 -> REPTIDNEST1 .   

REPTVARIABLEORFUNCTIONCALL -> IDNEST REPTVARIABLEORFUNCTIONCALL .  
REPTVARIABLEORFUNCTIONCALL ->  .  

FPARAMS -> id colon TYPE REPTFPARAMS3 REPTFPARAMS4  . 
FPARAMS ->  . 

FPARAMSTAIL -> comma id colon TYPE REPTFPARAMSTAIL4  . 

FUNCBODY -> lcurbr REPTFUNCBODY1 rcurbr  . 

FUNCDECL -> FUNCHEAD semi  . 

FUNCDEF -> FUNCHEAD FUNCBODY  . 

FUNCHEAD -> function id lpar FPARAMS rpar arrow RETURNTYPE  . 
FUNCHEAD -> constructor lpar FPARAMS rpar  . 

IDNEST -> dot id IDNEST2  .
IDNEST2 -> lpar APARAMS rpar  .
IDNEST2 -> REPTIDNEST1  .

IDORSELF -> id  .
IDORSELF -> self  . 

IMPLDEF -> implementation id lcurbr REPTIMPLDEF3 rcurbr  . 

INDICE -> lsqbr ARITHEXPR rsqbr  . 

LOCALVARDECL -> local VARDECL  . 

LOCALVARDECLORSTAT -> LOCALVARDECL  . 
LOCALVARDECLORSTAT -> STATEMENT  . 

MEMBERDECL -> FUNCDECL  . 
MEMBERDECL -> ATTRIBUTEDECL  . 

MULTOP -> mult  . 
MULTOP -> div  . 
MULTOP -> and  . 

OPTCLASSDECL2 -> isa id REPTOPTCLASSDECL22  . 
OPTCLASSDECL2 ->  . 

PROG -> REPTPROG0  . 

RELEXPR -> ARITHEXPR RELOP ARITHEXPR  . 

RELOP -> eq .
RELOP -> neq . 
RELOP -> lt . 
RELOP -> gt . 
RELOP -> leq . 
RELOP -> geq .

REPTAPARAMS1 -> APARAMSTAIL REPTAPARAMS1  . 
REPTAPARAMS1 ->  . 

REPTCLASSDECL4 -> VISIBILITY MEMBERDECL REPTCLASSDECL4  . 
REPTCLASSDECL4 ->  . 

REPTFPARAMS3 -> ARRAYSIZE REPTFPARAMS3  . 
REPTFPARAMS3 ->  . 

REPTFPARAMS4 -> FPARAMSTAIL REPTFPARAMS4  . 
REPTFPARAMS4 ->  . 

REPTFPARAMSTAIL4 -> ARRAYSIZE REPTFPARAMSTAIL4  . 
REPTFPARAMSTAIL4 ->  . 

REPTFUNCBODY1 -> LOCALVARDECLORSTAT REPTFUNCBODY1  . 
REPTFUNCBODY1 ->  . 

REPTIDNEST1 -> INDICE REPTIDNEST1  . 
REPTIDNEST1 ->  . 

REPTIMPLDEF3 -> FUNCDEF REPTIMPLDEF3  . 
REPTIMPLDEF3 ->  . 

REPTOPTCLASSDECL22 -> comma id REPTOPTCLASSDECL22  . 
REPTOPTCLASSDECL22 ->  . 

REPTPROG0 -> CLASSORIMPLORFUNC _addToList REPTPROG0  . 
REPTPROG0 ->  . 

REPTSTATBLOCK1 -> STATEMENT REPTSTATBLOCK1  . 
REPTSTATBLOCK1 ->  . 

REPTVARDECL3 -> ARRAYSIZE REPTVARDECL3  . 
REPTVARDECL3 ->  . 

RETURNTYPE -> TYPE  . 
RETURNTYPE -> void  . 

RIGHTRECARITHEXPR ->  . 
RIGHTRECARITHEXPR -> ADDOP TERM RIGHTRECARITHEXPR  . 

RIGHTRECTERM ->  . 
RIGHTRECTERM -> MULTOP FACTOR RIGHTRECTERM  . 

SIGN -> plus  . 
SIGN -> minus  . 

STATBLOCK -> lcurbr REPTSTATBLOCK1 rcurbr  . 
STATBLOCK -> STATEMENT  . 
STATBLOCK ->  . 

STATEMENT -> IDORSELF SELECTORLIST0 STATEMENTSUFFIX0 .
STATEMENT -> if lpar RELEXPR rpar then STATBLOCK else STATBLOCK semi  . 
STATEMENT -> while lpar RELEXPR rpar STATBLOCK semi  . 
STATEMENT -> read lpar VARIABLE rpar semi  . 
STATEMENT -> write lpar EXPR rpar semi  . 
STATEMENT -> return lpar EXPR rpar semi  . 

SELECTORLIST0 -> TYPESELECTOR SELECTORLIST0 . 
SELECTORLIST0 ->  .

TYPESELECTOR -> lsqbr ARITHEXPR rsqbr  .
TYPESELECTOR -> dot id  .

STATEMENTSUFFIX0 -> lpar APARAMS rpar semi  .
STATEMENTSUFFIX0 -> assign EXPR semi  .

TERM -> FACTOR RIGHTRECTERM  . 

TYPE -> int  . 
TYPE -> float  . 
TYPE -> id  . 

VARDECL -> id colon TYPE REPTVARDECL3 semi  . 

VARIABLE ->  IDORSELF VARIABLE2  .

VARIABLE2 ->  REPTIDNEST1 REPTVARIABLE  .
VARIABLE2 ->  lpar APARAMS rpar VARIDNEST  .   

REPTVARIABLE ->  VARIDNEST REPTVARIABLE  .
REPTVARIABLE ->   .

VARIDNEST -> dot id VARIDNEST2  .
VARIDNEST2 -> lpar APARAMS rpar VARIDNEST  .  
VARIDNEST2 -> REPTIDNEST1  .

VISIBILITY -> public  . 
VISIBILITY -> private  . 


